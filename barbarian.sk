options:
    soulboundText: "&9Soulbound"

    axeItem: wooden axe
    axeItem_type: "Barbarian's Axe"
    axeItemName: "<gradient:#A20000:#340000:#731313><bold>Barbarian's Axe"
    axeItemLore: "&7Grants absorption and regeneration on hit."
    initial_ability_cooldown: 8

on load:
    clear {barbarianClassItemNames::*}
    set {barbarianClassItemNames::*} to {@axeItem_type}

function upgradeAxe(p: player, i: item, newType: item, cooldown: number) :: item:
    send "&2Your %name of {_i}% &2glows brightly as it transforms!" to {_p}
    set {_oldLore::*} to lore of {_i}

    set {_newAxe} to unbreakable {_newType} named name of {_i}
    set lore of {_newAxe} to {_oldLore::*}
    set string tag "itemType" of custom nbt of {_newAxe} to {@axeItem_type}
    set double tag "kills" of custom nbt of {_newAxe} to getNumberTag({_i}, "kills")
    set double tag "cooldown" of custom nbt of {_newAxe} to {_cooldown}
    set boolean tag "isOnCooldown" of custom nbt of {_newAxe} to false
    set line 2 of lore of {_newAxe} to "&7Cooldown: %{_cooldown}% seconds"
    return {_newAxe}

on tool change:
    if name of new tool is {@axeItem_type}:
        apply a potion effect of slowness of tier 1 to the player for 10 days
            hide the particles
            hide the icon
    else:
        remove slowness from player

on death:
    if:
        attacker is a player
        isCorrectClass(attacker, "class.barbarian") is true
        isCorrectGear(attacker, attacker's tool, ({@axeItem_type})) is true
    then:
        set attacker's tool to updateKills(attacker's tool)

        # Determine if axe needs to be upgraded
        set {_kills} to getNumberTag(attacker's tool, "kills")
        
        # Wooden
        if {_kills} is 25:
            set attacker's tool to addEnchants(attacker, attacker's tool, (sharpness 1))
        # Copper
        else if {_kills} is 50:
            set attacker's tool to upgradeAxe(attacker, attacker's tool, copper axe, 7)
        else if {_kills} is 75:
            set attacker's tool to addEnchants(attacker, attacker's tool, (efficiency 1))
        # Golden
        else if {_kills} is 100:
            set attacker's tool to upgradeAxe(attacker, attacker's tool, golden axe, 6)
            set attacker's tool to addEnchants(attacker, attacker's tool, (bane of arthropods 1, smite 1))
        else if {_kills} is 125:
            set attacker's tool to addEnchants(attacker, attacker's tool, (looting 1, efficiency 1))
        # Iron
        else if {_kills} is 150:
            set attacker's tool to upgradeAxe(attacker, attacker's tool, iron axe, 5)
            set attacker's tool to addEnchants(attacker, attacker's tool, (bane of arthropods 1, smite 1, looting 2, efficiency 2))
        else if {_kills} is 200:
            set attacker's tool to addEnchants(attacker, attacker's tool, (bane of arthropods 2, smite 2))
        else if {_kills} is 250:
            set attacker's tool to addEnchants(attacker, attacker's tool, (thorns 1, efficiency 3))
        # Diamond
        else if {_kills} is 300:
            set attacker's tool to upgradeAxe(attacker, attacker's tool, diamond axe, 5)
            set attacker's tool to addEnchants(attacker, attacker's tool, (bane of arthropods 2, smite 2, thorns 1, looting 2, efficiency 3))
        else if {_kills} is 400:
            set attacker's tool to addEnchants(attacker, attacker's tool, (thorns 2, efficiency 4))
        else if {_kills} is 500:
            set attacker's tool to addEnchants(attacker, attacker's tool, (sharpness 1))
        else if {_kills} is 600:
            set attacker's tool to addEnchants(attacker, attacker's tool, (sharpness 2, bane of arthropods 3, smite 3, fire aspect 1, efficiency 4))
        # Netherite
        else if {_kills} is 700:
            set attacker's tool to upgradeAxe(attacker, attacker's tool, netherite axe, 4)
            set attacker's tool to addEnchants(attacker, attacker's tool, (sharpness 2, bane of arthropods 3, smite 3, thorns 2, fire aspect 1, looting 2, efficiency 4))
        else if {_kills} is 800:
            set attacker's tool to addEnchants(attacker, attacker's tool, (sharpness 3, thorns 3, fire aspect 1))
        else if {_kills} is 900:
            set attacker's tool to addEnchants(attacker, attacker's tool, (looting 3, efficiency 5))
        else if {_kills} is 1000:
            set attacker's tool to addEnchants(attacker, attacker's tool, (sharpness 5, bane of arthropods 5, smite 5, thorns 5, fire aspect 5, looting 5, efficiency 10))

on damage:
    if:
        attacker is a player
        isCorrectClass(attacker, "class.barbarian") is true
        isCorrectGear(attacker, attacker's tool, ({@axeItem_type})) is true
        isOnCooldown(attacker, attacker's tool) is false
    then:
        set boolean tag "isOnCooldown" of custom nbt of attacker's tool to true
        if attacker's tool is wooden axe:
            apply a potion effect of regeneration of tier 1 to attacker for 5 second
        if attacker's tool is copper axe:
            apply a potion effect of regeneration of tier 1 to attacker for 6 second
            apply a potion effect of regeneration of tier 1 to attacker for 5 second
        if attacker's tool is golden axe:
            apply a potion effect of absorption of tier 1 to attacker for 1.5 second
            apply a potion effect of regeneration of tier 1 to attacker for 7 second
        if attacker's tool is iron axe:
            apply a potion effect of absorption of tier 1 to attacker for 2.5 second
            apply a potion effect of regeneration of tier 1 to attacker for 10 second
        if attacker's tool is diamond axe:
            apply a potion effect of absorption of tier 2 to attacker for 3 second
            apply a potion effect of regeneration of tier 2 to attacker for 7.5 second
        if attacker's tool is netherite axe:
            apply a potion effect of absorption of tier 2 to attacker for 3 second
            apply a potion effect of regeneration of tier 3 to attacker for 3 second

        play sound "entity.ravager.roar" with volume 0.5 and pitch 1.5 to attacker
        set {_eye} to eye location of attacker
        set {_target} to location 1 block in front of {_eye}
        show 20 angry villager at {_target}

        set {_debug} to getTimespanTag(attacker's tool, "cooldown")
        send "debug: waiting for %{_debug}%" to all players
        wait getTimespanTag(attacker's tool, "cooldown")
        send action bar "&a&lAbility Ready!" to attacker
        play sound "block.note_block.chime" with pitch 2 to attacker
        set boolean tag "isOnCooldown" of custom nbt of attacker's tool to false

command /givebarbarianaxe:
    permission: op
    trigger:
        set {_name} to mini message from {@axeItemName}
        set {_axe} to createClassItem({@axeItem}, {_name}, {@axeItemLore}, "&cKills", {@initial_ability_cooldown})
        set string tag "itemType" of custom nbt of {_axe} to {@axeItem_type}
        set double tag "kills" of custom nbt of {_axe} to 0
        set double tag "cooldown" of custom nbt of {_axe} to {@initial_ability_cooldown}
        set boolean tag "isOnCooldown" of custom nbt of {_axe} to false
        give {_axe} to player

        # send "&6&lYour prayers have been answered..." to player
        # apply a potion effect of nausea of tier 1 to the player for 9 seconds
        # apply a potion effect of blindness of tier 1 to the player for 6 seconds
        # play sound "entity.piglin_brute.angry" with volume 4 and pitch 0.5 to player
        # play sound "entity.ghast.ambient" with volume 4 and pitch 0.5 to player
        # play 1 end rod above player's head
        # wait 50 ticks
        # play 1 end rod above player's head
        # wait 50 ticks
        # play 1 end rod above player's head
        # wait 50 ticks