options:
    soulboundText: "&9Soulbound"

    slingshot_type: "Slingshot"
    slingshot_name_arrow: "<gradient:#ffb28b:#ffb28b:#ff5b5b:#ff0000><bold>Slingshot Ammo: Arrow"
    slingshot_name_snowball: "<gradient:#ffb28b:#ffb28b:#5be1ff:#00bfff><bold>Slingshot Ammo: Snowball"
    slingshot_name_firecharge: "<gradient:#ff9b5b:#ffb28b:#ffb28b:#ff5b5b><bold>Slingshot Ammo: Fireball"
    slingshot_name_firework: "<gradient:#ffb28b:#ffb28b:#ff5b5b:#ff0000><bold>Slingshot Ammo: Firework"
    slingshot_lore: "&7Right-click to shoot, left-click to change ammo type."
    initial_arrow_cooldown: 1.5
    initial_snowball_cooldown: 2.5
    initial_firecharge_cooldown: 6
    initial_firework_cooldown: 2
    firework_range: 30

on load:
    clear {rangerClassItemNames::*}
    set {rangerClassItemNames::*} to {@slingshot_type}

function upgradeSlingshot(player: player, i: item) :: item:
    set {_newLevel} to (getNumberTag({_i}, "level") + 1)

    send "You feel your dexterity increase as you become more familiar with your Slingshot! - &5Level %{_newLevel}%" to {_player}

    set double tag "level" of custom nbt of {_i} to {_newLevel}
    set double tag "arrowCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "arrowCooldown") - 0.2)
    set line 1 of lore of {_i} to "&7Level %{_newLevel}%: %{@slingshot_lore}%"

    if {_newLevel} is 1:
        # User can't switch weapons to refresh cooldown, so we update cooldown display manually
        set double tag "cooldown" of custom nbt of {_i} to getNumberTag({_i}, "arrowCooldown")
        set {_cooldown} to double tag "cooldown" of custom nbt of {_i}
        set line 2 of lore of {_i} to "&7Cooldown: %{_cooldown}% seconds"
    if {_newLevel} is 2:
        send "Your Slingshot looks like it could shoot &5Snowballs &7now..." to {_player}
        set string list tag "availableAmmo" of custom nbt of {_i} to "arrow", and "snowball"
    if {_newLevel} is 3:
        send "Your Slingshot looks like it could shoot &5Fireballs &7now..." to {_player}
        set string list tag "availableAmmo" of custom nbt of {_i} to "arrow", and "snowball", and "firecharge"
        set double tag "snowballCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "snowballCooldown") - 0.5)
    if {_newLevel} is 4:    
        send "Your Slingshot looks like it could shoot &5Fireworks &7now..." to {_player}
        set string list tag "availableAmmo" of custom nbt of {_i} to "arrow", and "snowball", and "firecharge", and "firework"
        set double tag "snowballCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "snowballCooldown") - 0.5)
        set double tag "firechargeCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "firechargeCooldown") - 1)
    if {_newLevel} is 5:    
        set double tag "snowballCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "snowballCooldown") - 0.5)
        set double tag "firechargeCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "firechargeCooldown") - 1)
        set double tag "fireworkCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "fireworkCooldown") - 1)
    return {_i}

on projectile hit:
    if metadata value "projectileOwner" of projectile exists:
        set {_owner} to metadata value "projectileOwner" of projectile
        set metadata value "rangerLastDamager" of victim to {_owner}
        if projectile is a firework rocket:
            create a safe explosion of force 2 at location of projectile
            delete projectile

        if victim is a living entity:
            if projectile is a snowball:
                damage victim by 2
                apply slowness 2 to victim for 4 seconds
            if projectile is a fireball:
                ignite victim for 4 seconds

        set {_attacker} to metadata value "projectileOwner" of projectile
        # Damage takes a bit to register
        wait 1 ticks
        if health of victim is 0:
            if:
                {_attacker} is a player
                isCorrectClass({_attacker}, "class.ranger") is true
                isCorrectGear({_attacker}, {_attacker}'s tool, ({@slingshot_type}), false) is true
            then:
                # Update stats
                set {_attacker}'s tool to updateKills({_attacker}'s tool)
                
                # Determine if slingshot needs to be upgraded
                set {_totalKills} to getNumberTag({_attacker}'s tool, "kills")
                set {_lastMilestone} to getNumberTag({_attacker}'s tool, "lastMilestone")
                
                # slingshot upgrades once 3 times the previous milestone is reached
                if:
                    getNumberTag({_attacker}'s tool, "level") < 5
                    {_totalKills} >= ({_lastMilestone} * 3)
                then:
                    set {_attacker}'s tool to upgradeSlingshot({_attacker}, {_attacker}'s tool)
                    set double tag "lastMilestone" of custom nbt of {_attacker}'s tool to ({_lastMilestone} * 3)

on leftclick:
    if:
        isCorrectClass(player, "class.ranger") is true
        isCorrectGear(player, player's tool, {rangerClassItemNames::*}, false) is true
    then:
        if isCorrectGear(player, player's tool, ({@slingshot_type}), false) is true:
            # Player is using one of their class items, cancel the default right click event
            cancel event
            set {_availableAmmoTypes::*} to string list tag "availableAmmo" of custom nbt of player's tool
            loop {_availableAmmoTypes::*}:
                if string tag "currentAmmoType" of custom nbt of player's tool is loop-value:
                    # Get next ammo type
                    set {_index} to loop-index parsed as number
                    set {_nextIndex} to {_index} + 1

                    # Wrap around if at end of list
                    set {_size} to size of {_availableAmmoTypes::*}
                    if {_nextIndex} > size of {_availableAmmoTypes::*}:
                        set {_nextIndex} to 1

                    set {_nextAmmoType} to {_availableAmmoTypes::%{_nextIndex}%}
                    set string tag "currentAmmoType" of custom nbt of player's tool to {_nextAmmoType}
                    switch {_nextAmmoType}:
                        case "arrow":
                            set {_newName} to mini message from {@slingshot_name_arrow}
                            set {_arrowCooldown} to double tag "arrowCooldown" of custom nbt of player's tool
                            set double tag "cooldown" of custom nbt of player's tool to {_arrowCooldown}
                            set line 2 of lore of player's tool to "&7Cooldown: %{_arrowCooldown}% seconds"
                        case "snowball":
                            set {_newName} to mini message from {@slingshot_name_snowball}
                            set {_snowballCooldown} to double tag "snowballCooldown" of custom nbt of player's tool
                            set double tag "cooldown" of custom nbt of player's tool to {_snowballCooldown}
                            set line 2 of lore of player's tool to "&7Cooldown: %{_snowballCooldown}% seconds"
                        case "firecharge":
                            set {_newName} to mini message from {@slingshot_name_firecharge}
                            set {_firechargeCooldown} to double tag "firechargeCooldown" of custom nbt of player's tool
                            set double tag "cooldown" of custom nbt of player's tool to {_firechargeCooldown}
                            set line 2 of lore of player's tool to "&7Cooldown: %{_firechargeCooldown}% seconds"
                        case "firework":
                            set {_newName} to mini message from {@slingshot_name_firework}
                            set {_fireworkCooldown} to double tag "fireworkCooldown" of custom nbt of player's tool
                            set double tag "cooldown" of custom nbt of player's tool to {_fireworkCooldown}
                            set line 2 of lore of player's tool to "&7Cooldown: %{_fireworkCooldown}% seconds"

                    set name of player's tool to {_newName}
                    play sound "item.book.page_turn" with volume 1 and pitch 1.2 to player
                    stop loop

on rightclick:
    if:
        isCorrectClass(player, "class.ranger") is true
        isCorrectGear(player, player's tool, {rangerClassItemNames::*}, false) is true
    then:
        if isCorrectGear(player, player's tool, ({@slingshot_type}), false) is true:
            # Player is using one of their class items, cancel the default right click event
            cancel event

            set {_ammoType} to string tag "currentAmmoType" of custom nbt of player's tool

            if metadata value "is%{_ammoType}%OnCooldown" of player exists:
                send action bar "&5%{_ammoType}% &7is on cooldown!" to player
                stop

            set metadata value "is%{_ammoType}%OnCooldown" of player to true
            set {_projectileSpawn} to location 1 block in front of player's eyes

            switch {_ammoType}:
                case "arrow":
                    spawn an arrow at {_projectileSpawn}
                    set {_arrow} to last spawned entity
                    set yaw of {_arrow} to yaw of player
                    set pitch of {_arrow} to pitch of player
                    set {_v} to vector in direction of player
                    set velocity of {_arrow} to {_v} * 2
                    set metadata value "projectileOwner" of {_arrow} to player
                    play sound "entity.arrow.shoot" with volume 1 and pitch 1.2 to player

                case "snowball":
                    spawn a snowball at {_projectileSpawn}
                    set {_snowball} to last spawned entity
                    set {_v} to vector in direction of player
                    set velocity of {_snowball} to {_v} * 1.75
                    set metadata value "projectileOwner" of {_snowball} to player
                    play sound "entity.snowball.throw" with volume 1 and pitch 1.2 to player

                case "firecharge":
                    spawn a fireball at {_projectileSpawn}
                    set {_firecharge} to last spawned entity
                    set {_v} to vector in direction of player
                    set velocity of {_firecharge} to {_v} * 0.8
                    set metadata value "projectileOwner" of {_firecharge} to player
                    play sound "entity.blaze.shoot" with volume 1 and pitch 1 to player

                case "firework":
                    spawn a firework rocket at {_projectileSpawn}
                    set {_rocket} to last spawned entity
                    set {_v} to vector in direction of player
                    set y-coordinate of {_v} to 0
                    set velocity of {_rocket} to {_v} * 2
                    set metadata value "projectileOwner" of {_rocket} to player
                    set metadata value "projectileStart" of {_rocket} to location of {_rocket}
                    set metadata value "projectileRange" of {_rocket} to {@firework_range}

            set {_c} to getTimespanTag(player's tool, "cooldown")
            wait {_c}
            send action bar "&5%{_ammoType}% &7is ready to fire!" to player
            play sound "block.note_block.chime" with volume 0.2 and pitch 0.75 to player
            delete metadata value "is%{_ammoType}%OnCooldown" of player
            delete metadata value "isfireworkOnCooldown" of player

command /giverangerItems:
    permission: op
    trigger:
        set {_name} to mini message from {@slingshot_name_arrow}
        set {_slingshot} to createClassItem(lead, {_name}, {@slingshot_lore}, "&cKills", {@initial_arrow_cooldown})
        set string tag "itemType" of custom nbt of {_slingshot} to {@slingshot_type}
        set string tag "currentAmmoType" of custom nbt of {_slingshot} to "arrow"
        set string list tag "availableAmmo" of custom nbt of {_slingshot} to "arrow"
        set double tag "cooldown" of custom nbt of {_slingshot} to {@initial_arrow_cooldown}
        set double tag "arrowCooldown" of custom nbt of {_slingshot} to {@initial_arrow_cooldown}
        set double tag "snowballCooldown" of custom nbt of {_slingshot} to {@initial_snowball_cooldown}
        set double tag "firechargeCooldown" of custom nbt of {_slingshot} to {@initial_firecharge_cooldown}
        set double tag "fireworkCooldown" of custom nbt of {_slingshot} to {@initial_firework_cooldown}
        set double tag "level" of custom nbt of {_slingshot} to 0
        set double tag "maxLevel" of custom nbt of {_slingshot} to 5
        set double tag "lastMilestone" of custom nbt of {_slingshot} to 25
        give {_slingshot} to player

command /clearrangermetadata:
    permission: class.ranger
    trigger:
        delete metadata value "is%{_ammoType}%OnCooldown" of player
        delete metadata value "isarrowOnCooldown" of player
        delete metadata value "issnowballOnCooldown" of player
        delete metadata value "isfirechargeOnCooldown" of player
        delete metadata value "isfireworkOnCooldown" of player
        send "Cleared metadata." to player