options:
    soulboundText: "&9Soulbound"

    slingshot_type: "Slingshot"
    slingshot_name_firework: "<gradient:#ffb28b:#ffb28b:#ff5b5b:#ff0000><bold>Slingshot Ammo: Firework"
    slingshot_name_snowball: "<gradient:#ffb28b:#ffb28b:#5be1ff:#00bfff><bold>Slingshot Ammo: Snowball"
    slingshot_lore: "&7Right-click to shoot, left-click to change ammo type."
    initial_firework_cooldown: 2
    initial_snowball_cooldown: 2.5

on load:
    clear {rangerClassItemNames::*}
    set {rangerClassItemNames::*} to {@slingshot_type}
    set {rangerAvailableAmmoTypes::*} to "firework", and "snowball"

function upgradeSlingshot(player: player, i: item) :: item:
    set {_newLevel} to (getNumberTag({_i}, "level") + 1)

    send "You feel your dexterity increase as you become more familiar with your Slingshot! - &5Level %{_newLevel}%" to {_player}

    set double tag "level" of custom nbt of {_i} to {_newLevel}
    set double tag "fireworkCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "fireworkCooldown") - 1)
    set double tag "snowballCooldown" of custom nbt of {_i} to (getNumberTag({_i}, "snowballCooldown") - 1)
    set line 1 of lore of {_i} to "&7Level %{_newLevel}%: %{@slingshot_lore}%"

    # if {_newLevel} is 2:
    # if {_newLevel} is 3:
    # if {_newLevel} is 4:    
    # if {_newLevel} is 5:    
    return {_i}

# on damage:
#     if:
#         attacker is a player
#         isCorrectClass(attacker, "class.ranger") is true
#         isCorrectGear(attacker, attacker's tool, ({@slingshot_type}), false) is true
#     then:

on death:
    if:
        attacker is a player
        isCorrectClass(attacker, "class.ranger") is true
        isCorrectGear(attacker, attacker's tool, ({@slingshot_type}), false) is true
    then:
        # Update stats
        set attacker's tool to updateKills(attacker's tool)
        
        # Determine if slingshot needs to be upgraded
        set {_totalKills} to getNumberTag(attacker's tool, "kills")
        set {_lastMilestone} to getNumberTag(attacker's tool, "lastMilestone")
        
        # slingshot upgrades once 3 times the previous milestone is reached
        if:
            getNumberTag(attacker's tool, "level") < 5
            {_totalKills} >= ({_lastMilestone} * 3)
        then:
            set attacker's tool to upgradeSlingshot(attacker, attacker's tool)
            set double tag "lastMilestone" of custom nbt of attacker's tool to ({_lastMilestone} * 3)

on leftclick:
    if:
        isCorrectClass(player, "class.ranger") is true
        isCorrectGear(player, player's tool, {rangerClassItemNames::*}, false) is true
    then:
        if:
            isCorrectGear(player, player's tool, ({@slingshot_type}), false) is true
            isOnCooldown(player, player's tool) is false
        then:
            # Player is using one of their class items, cancel the default right click event
            cancel event
            loop {rangerAvailableAmmoTypes::*}:
                if string tag "currentAmmoType" of custom nbt of player's tool is loop-value:
                    # Get next ammo type
                    set {_index} to loop-index parsed as number
                    set {_nextIndex} to {_index} + 1

                    send "Debug: current index %{_index}%, next index %{_nextIndex}%" to player

                    # Wrap around if at end of list
                    if {_nextIndex} > size of {rangerAvailableAmmoTypes::*}:
                        set {_nextIndex} to 1

                    set {_nextAmmoType} to {rangerAvailableAmmoTypes::%{_nextIndex}%}
                    set string tag "currentAmmoType" of custom nbt of player's tool to {_nextAmmoType}
                    switch {_nextAmmoType}:
                        case "firework":
                            set {_newName} to mini message from {@slingshot_name_firework}
                            set {_fireworkCooldown} to double tag "fireworkCooldown" of custom nbt of player's tool
                            set double tag "cooldown" of custom nbt of player's tool to {_fireworkCooldown}
                            set line 2 of lore of player's tool to "&7Cooldown: %{_fireworkCooldown}% seconds"
                        case "snowball":
                            set {_newName} to mini message from {@slingshot_name_snowball}
                            set {_snowballCooldown} to double tag "snowballCooldown" of custom nbt of player's tool
                            set double tag "cooldown" of custom nbt of player's tool to {_snowballCooldown}
                            set line 2 of lore of player's tool to "&7Cooldown: %{_snowballCooldown}% seconds"

                    set name of player's tool to {_newName}
                    send "Switched slingshot ammo to &5%{_nextAmmoType}%!" to player
                    play sound "item.book.page_turn" with volume 1 and pitch 1.2 to player
                    stop loop

on rightclick:
    if:
        isCorrectClass(player, "class.ranger") is true
        isCorrectGear(player, player's tool, {rangerClassItemNames::*}, false) is true
    then:
        if:
            isCorrectGear(player, player's tool, ({@slingshot_type}), false) is true
            isOnCooldown(player, player's tool) is false
        then:
            # Player is using one of their class items, cancel the default right click event
            cancel event

            set {_ammoType} to string tag "currentAmmoType" of custom nbt of player's tool

            switch {_ammoType}:
                case "firework":
                    shoot firework rocket from player
                    play sound "entity.firework_rocket.launch" with volume 1 and pitch 1.2 to player
                case "snowball":
                    shoot snowball from player with speed 1.5
                    play sound "entity.snowball.throw" with volume 1 and pitch 1.2 to player

            set boolean tag "isOnCooldown" of custom nbt of player's tool to true
            set {_c} to getTimespanTag(player's tool, "cooldown")
            wait {_c}
            send action bar "Ready to fire!" to player
            play sound "block.note_block.chime" with pitch 2 to player
            set boolean tag "isOnCooldown" of custom nbt of player's tool to false

command /giverangerItems:
    permission: op
    trigger:
        set {_name} to mini message from {@slingshot_name_firework}
        set {_slingshot} to createClassItem(lead, {_name}, {@slingshot_lore}, "&cKills", {@initial_firework_cooldown})
        set string tag "itemType" of custom nbt of {_slingshot} to {@slingshot_type}
        set string tag "currentAmmoType" of custom nbt of {_slingshot} to "firework"
        set double tag "cooldown" of custom nbt of {_slingshot} to {@initial_firework_cooldown}
        set double tag "fireworkCooldown" of custom nbt of {_slingshot} to {@initial_firework_cooldown}
        set double tag "snowballCooldown" of custom nbt of {_slingshot} to {@initial_snowball_cooldown}
        set double tag "level" of custom nbt of {_slingshot} to 0
        set double tag "maxLevel" of custom nbt of {_slingshot} to 5
        set double tag "lastMilestone" of custom nbt of {_slingshot} to 25
        give {_slingshot} to player
